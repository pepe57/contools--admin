@echo off

rem Description:
rem   Script disables telemetry junk from Microsoft and others...
rem

setlocal

call "%%~dp0..\__init__\__init__.bat" || exit /b

rem script names call stack, disabled due to self call and partial inheritance (process elevation does not inherit a parent process variables by default)
rem if defined ?~ ( set "?~=%?~%-^>%~nx0" ) else if defined ?~nx0 ( set "?~=%?~nx0%-^>%~nx0" ) else set "?~=%~nx0"
set "?~=%~nx0"

if 0%IMPL_MODE% NEQ 0 goto IMPL
"%USERBIN_SCRIPTS_BAT_ROOT%/runas/hta/cmd-admin.bat" /c @set "IMPL_MODE=1" ^& "%~f0" %*
exit /b

:IMPL
call "%%CONTOOLS_ROOT%%/std/is_admin_elevated.bat" || (
  echo;%?~%: error: process must be Administrator account elevated to continue.
  exit /b 255
) >&2

rem Under WOW64 (32-bit process in 64-bit Windows) restart script in 64-bit mode
if "%PROCESSOR_ARCHITECTURE%" == "AMD64" goto X64
if not defined PROCESSOR_ARCHITEW6432 goto X32

rem restart in x64
if exist "%SystemRoot%\Sysnative\*" (
  call :CMD "%%SystemRoot%%\Sysnative\cmd.exe" /c @%%0 %%*
  exit /b
)

(
  echo;%?~%: error: run script in 64-bit console ONLY (in administrative mode)!
  exit /b 255
) >&2

:X64
:X32

if not defined RETAKEOWNER_EXE if defined CONTOOLS_UTILS_BIN_ROOT set "RETAKEOWNER_EXE=%CONTOOLS_UTILS_BIN_ROOT%/retakeowner.exe"

set "COMPATTELRUNNER_LOG_DIR=%ProgramData%\Microsoft\Diagnosis\ETLLogs\AutoLogger"

echo Stopping and disabling CompatTelRunner.exe services..
call :CMD sc stop DiagTrack
call :CMD sc config DiagTrack start= disabled
call :CMD sc stop dmwappushservice
call :CMD sc config dmwappushservice start= disabled
echo;

echo Stopping and disabling NvTelemetryContainer.exe services..
call :CMD sc stop NvTelemetryContainer
call :CMD sc config NvTelemetryContainer start= disabled
echo;

echo Updating CompatTelRunner.exe files..
if not exist "%COMPATTELRUNNER_LOG_DIR%\AutoLogger-Diagtrack-Listener.bak" (
  copy /Y /B "%COMPATTELRUNNER_LOG_DIR%\AutoLogger-Diagtrack-Listener.etl" "%COMPATTELRUNNER_LOG_DIR%\AutoLogger-Diagtrack-Listener.bak"
)
echo;> "%COMPATTELRUNNER_LOG_DIR%\AutoLogger-Diagtrack-Listener.etl"
echo;

echo Updating CompatTelRunner.exe registry..
call :CMD reg add "HKEY_LOCAL_MACHINE\SOFTWARE\Policies\Microsoft\Windows\DataCollection" /v AllowTelemetry /t REG_DWORD /d 0 /f
call :CMD reg add "HKEY_LOCAL_MACHINE\SOFTWARE\Microsoft\Windows NT\CurrentVersion\Image File Execution Options\CompatTelRunner.exe" /v Debugger /t REG_SZ /d "systray.exe" /f
echo;

rem CAUTION:
rem   1. If a variable is empty, then it would not be expanded in the `cmd.exe`
rem      command line or in the inner expression of the
rem      `for /F "usebackq ..." %%i in (`<inner-expression>`) do ...`
rem      statement.
rem   2. The `cmd.exe` command line or the inner expression of the
rem      `for /F "usebackq ..." %%i in (`<inner-expression>`) do ...`
rem      statement does expand twice.
rem
rem   We must expand the command line into a variable to avoid these above.
rem
set ?.=@dir "%SystemRoot%\CompatTelRunner.exe" /A:-D /B /O:N /S 2^>nul

echo Updating CompatTelRunner.exe permissions...
for /F "usebackq tokens=* delims="eol^= %%i in (`%%?.%%`) do set "FILE=%%i" & call :UPDATE_PERMISSIONS
echo;

exit /b

:UPDATE_PERMISSIONS
echo;^>%FILE%

if exist "%RETAKEOWNER_EXE%" goto RETAKEOWNER_WORKAROUND

rem CAUTION: Obsolete implementation, `takeown` and `icacls` does not work anymore on TrustedInstaller protected files!
echo;%?~%: warning: system takeown utility may fail to take ownership on TrustedInstaller protected files beginning from Windows 7. Copy `retakeowner.exe` utility into directory with the script to bypass this issue.
call :CMD "%%SystemRoot%%\System32\takeown.exe" /S localhost /U "%%USERNAME%%" /F "%%FILE%%"

goto RETAKEOWNER_WORKAROUND_END

:RETAKEOWNER_WORKAROUND
call :CMD "%%RETAKEOWNER_EXE%%" "%%FILE%%" "%%USERNAME%%"
echo;%?~%: retakeowner last error code: %ERRORLEVEL%

:RETAKEOWNER_WORKAROUND_END

call :CMD "%%SystemRoot%%\System32\icacls.exe" "%%FILE%%" /remove:g "NT SERVICE\TrustedInstaller"
call :CMD "%%SystemRoot%%\System32\icacls.exe" "%%FILE%%" /deny "*S-1-1-0:(WO,GE)"

echo;

exit /b

:CMD
echo;^>%*
(
  %*
)
