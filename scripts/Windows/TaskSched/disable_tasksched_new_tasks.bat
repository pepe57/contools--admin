@echo off

rem Description:
rem   Script disables the Task Scheduler of a new task creation for everyone.
rem
rem   Based on:
rem     https://superuser.com/questions/1222551/how-to-prevent-programs-from-making-new-entries-in-task-scheduler-when-installin/1237127#1237127

setlocal

call "%%~dp0..\__init__\__init__.bat"

rem script names call stack, disabled due to self call and partial inheritance (process elevation does not inherit a parent process variables by default)
rem if defined ?~ ( set "?~=%?~%-^>%~nx0" ) else if defined ?~nx0 ( set "?~=%?~nx0%-^>%~nx0" ) else set "?~=%~nx0"
set "?~=%~nx0"

if 0%IMPL_MODE% NEQ 0 goto IMPL
"%USERBIN_SCRIPTS_BAT_ROOT%/runas/hta/cmd-admin.bat" /c @set "IMPL_MODE=1" ^& "%~f0" %*
exit /b

:IMPL
call "%%CONTOOLS_ROOT%%/std/is_admin_elevated.bat" || (
  echo;%?~%: error: process must be System account elevated to continue.
  exit /b 255
) >&2

call :CMD "%%SystemRoot%%\System32\icacls.exe" "%%SystemRoot%%\System32\Tasks" /deny "*S-1-1-0:(WD)"

echo;

pause

exit /b

:CMD
echo;^>%*
(
  %*
)
